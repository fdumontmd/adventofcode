work day part:
    cargo watch -x "check -p {{day}}" -s "just test {{day}}" -s "just lint {{day}}" -s "just bench {{day}} {{part}}" -s "just flamegraph {{day}} {{part}}"
lint day:
    clippy-tracing --action check --exclude target --exclude benches --exclude .daily-template
    cargo clippy -p {{day}}
test day:
    cargo nextest run -p {{day}}
test-all:
    cargo nextest run
bench-all:
    cargo bench -q > benchmarks.txt
bench day part:
    cargo bench --bench {{day}} {{part}} >> {{day}}.bench.txt
flamegraph day part:
    cargo flamegraph --profile flamegraph --root --package {{day}} --bin {{part}} -o flamegraphs/{{day}}--{{part}}.svg
dhat day part:
    cargo run --profile dhat --features dhat-heap --package {{day}} --bin {{part}}
create day:
    cargo generate --path ./.daily-template --name {{day}}
